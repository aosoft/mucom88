#include "hspinet.as"

#packopt name "updcheck"
#packopt icon "mucom88win.ico"
#packopt hide 1
#packopt xsize 480
#packopt ysize 300

	;	mucom88 version check
	;
	title "Update checker"
	;
	sdim buf,$4000
	sdim sbuf,$4000
	sdim s1,$100
	sdim s2,$100
	prever="Ver0."
	weburl="https://onitama.tv/mucom88/"
	;
	s1 = dir_cmdline
	myver=0+s1
	if myver<=0 : dialog "MUCOM88から起動してください" : end
	;
	wx=480:wy=300
	;
	netinit
	if stat : end		; ネットワークに接続していない

	;	URLを指定
	neturl "http://onitama.tv/mucom88/"
	;	GET形式でCGIにパラメーターを渡す
	netrequest_get "vernotice.txt"

*main
	;	結果待ちのためのループ
	netexec res
	if res > 0 : goto *comp
	if res < 0 : goto *bad
	await 50
	goto *main

*bad
	;	エラー
	neterror estr
	end

*comp
	;	完了
	;
	netgetv buf
	nkfcnv sbuf,buf,"sW"		; UTF8->SJISに変換
	;
	notesel sbuf
	noteget s1,0
	i=instr(s1,0,prever)
	if i<0 : end
	;
	s2=strmid(s1,i+strlen(prever),64)
	newver=0+s2
	;
	if myver>=newver : end		; 自分はすでに最新バージョン
	;
	screen 0,wx,wy
	gsel 0,1
	syscolor 15:boxf
	sysfont 17:color 0,0,0
	mesbox sbuf,wx,240,1
	;
	pos 0,248
	mes "※ アップデートは、MUCOM88 Windowsを終了した後、以前のバージョンを削除した上で"
	mes "   新しいバージョンをインストールしてください。"
	objsize wx,24
	button "MUCOM88 ダウンロードサイトを開く",*goweb
	;
	stop

*goweb
	exec weburl,16
	stop
